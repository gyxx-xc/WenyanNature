// TODO: rewrite all of these sometime

plugins {
    id 'java'
    id 'idea' // for JUnit
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.140'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = 'https://maven.blamejared.com' }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(25)

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    distributionType = Wrapper.DistributionType.BIN
}

base {
    archivesName = mod_id
}

version = mod_version
group = mod_group_id

sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
    gameTest {
        resources {
            srcDirs 'src/gameTest/generated'
        }
        compileClasspath += sourceSets.main.output
    }
}

neoForge {
    enable {
        version = neo_version
        enabledSourceSets = sourceSets // All source sets use Minecraft code
    }

    mods {
        wenyan_programming {
            sourceSet(sourceSets.main)
        }
        wenyanTest {
            sourceSet(sourceSets.gameTest)
        }
    }

//    parchment {
//        mappingsVersion = project.parchment_mappings_version
//        minecraftVersion = project.parchment_minecraft_version
//    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            loadedMods = [mods.wenyan_programming]
            // logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            sourceSet = sourceSets.gameTest
            loadedMods.add(mods.wenyanTest)
        }

        clientData {
            clientData()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        serverData {
            serverData()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

    }

    unitTest {
        enable()
        testedMod = mods."${mod_id}"
    }
}

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    implementation project(":judou")
    jarJar implementation(project(":judou"))

    // for annotation processor
    compileOnly project(":language_processor")
    annotationProcessor project(":language_processor")
    // for ap's unit test
    testCompileOnly project(":language_processor")
    testAnnotationProcessor project(":language_processor")

    // mod requirements
//    implementation "me.shedaniel.cloth:cloth-config-neoforge:15.0.140"
//    compileOnly "vazkii.patchouli:Patchouli:1.21.1-92-NEOFORGE:api"
//    runtimeOnly "vazkii.patchouli:Patchouli:1.21.1-92-NEOFORGE"

    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    // copy from https://github.com/AppliedEnergistics/Applied-Energistics-2/blob/main/build.gradle
    testImplementation(platform("org.junit:junit-bom:6.0.2"))
    testImplementation(platform("org.assertj:assertj-bom:3.27.6"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.platform:junit-platform-launcher")
    testImplementation("org.assertj:assertj-core")
    testImplementation("com.google.guava:guava-testlib:21.0")

    // for game test
    gameTestImplementation("net.neoforged:testframework:${neo_version}")
    gameTestImplementation(project(":judou"))
    
    // allow unit tests to load without testframework NoClassDefFoundError when NeoForge test runner bootstaps all mod source sets
    testImplementation("net.neoforged:testframework:${neo_version}")
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven { url = "file://${project.projectDir}/repo" }
    }
}

// copy from https://github.com/junit-team/junit-examples/blob/r6.0.2/junit-jupiter-starter-gradle/build.gradle
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
